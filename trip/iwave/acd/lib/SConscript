import os

imports = ' libname cpplist srclist'

try:  # madagascar version
    Import('env root pkgdir libdir' + imports)
    env = env.Clone()
    env.Append(CPPPATH = cpplist)
    CC = env.get('CC','gcc')
    if CC.rfind('gcc') >= 0:
        gcc_version = float(os.popen(CC + " -dumpversion | cut -f1,2 -d.").read().rstrip())
        if gcc_version > 4.2:
            env.Append(CFLAGS = ' -msse4.2')
except:  # autonomous version    
    Import('vars' + imports)
    root = None
    env = Environment(ENV = os.environ,
                      variables = vars,
                       CC={'CC' : '${CC}'},
                       CFLAGS={'CFLAGS' : '${CFLAGS}'},
                       CCFLAGS={'CCFLAGS' : '${CCFLAGS}'},
                       CXX={'CXX' : '${CXX}'},
                       CXXFLAGS={'CXXFLAGS' : '${CXXFLAGS}'},
                       CPPPATH = cpplist)

# create list of source files found in prescribed source directories
srcs=[]
for dir in srclist:
    srcs=srcs+Glob(os.path.join('..',dir,'*.c'))
    srcs=srcs+Glob(os.path.join('..',dir,'*.cc'))
    srcs=srcs+Glob(os.path.join('..',dir,'*.cpp'))

    # Solaris
    if not env['PLATFORM'] in ('linux','posix','linux2','darwin'):
        for x in Glob(os.path.join('..',dir,'CFL_4_*.c')):
            if x in srcs:
                srcs.remove(x)
    
# create library
if len(srcs) > 0:
    env.Library(libname,srcs)
