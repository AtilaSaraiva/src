import os, sys, string, re
sys.path.append('../../framework')
import bldutil

src = 'convert iwave_fopen mm_malloc parser utils'

try: # distribution version
    Import('env root libdir incdir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    root = None

##############################################################################
# INCLUDES
##############################################################################

objects = []
includes = []
for source in Split(src):
    inc = env.RSF_Include(source,prefix='sf_') # generate header
    obj = env.StaticObject(source+'.c')        # compile
    env.Depends(obj,inc)
    
    objects.append(obj)
    includes.append(inc)

##############################################################################
# LIBRARY
##############################################################################
lib = env.StaticLibrary('tripbase',objects)

inc = env.Command('base.h',includes,Action(bldutil.__merge))

env.Install('../../lib',lib)
env.Install('../../include/trip',inc)

if root:
    env.Install(libdir,lib)
    env.Install(os.path.join(incdir,'trip'),inc)

##############################################################################
# TESTS
##############################################################################

for prog in Split('iwave_fopen',):
    sources = ['Test' + prog,prog]
    bldutil.depends(env,sources,prog)
    sources = map(lambda x: x + '.o',sources)
    env.Object('Test' + prog + '.c')
    env.Program(sources,PROGPREFIX='',PROGSUFFIX='.x')

