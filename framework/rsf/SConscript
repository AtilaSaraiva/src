import os, sys
import bldutil, rsf.doc

################################################################################

Import('env bindir pkgdir system user dotproj')

env.Install(pkgdir,'__init__.py')
env.Install(os.path.join(pkgdir,'user'),'__init__.py')

dirs = Split('plot libplot plplot pens sumain suplot') + system + user

for usr in user:
    py = 'sf'+usr+'.py'
    env.Install(os.path.join('.',py),
                os.path.join('../../user',usr,py))
#    bldutil.py_install(py, env, pkgdir)

env.Command('prog.py',map(lambda x: 'sf'+x+'.py',dirs),
            action=Action(rsf.doc.getprogs))
env.Depends('prog.py',['vpplot.py','doc.py'])

env.Command('use.py',map(lambda x: '#/'+x,dotproj),
            action=Action(rsf.doc.use))
env.Depends('use.py',['prog.py','conf.py','doc.py'])

for src in Split('path doc flow proj prog tex book suproj conf use'):
    bldutil.py_install(src, env, pkgdir)

for src in Split('latex2wiki sftour'):
    bldutil.py_install(src, env, pkgdir)
    env.Command(src,src+'.py','cp $SOURCE $TARGET')
    env.Install(bindir,src)

env.Install(bindir,'sfdoc')

##########################################################################
# Self-documentation

Doc = Builder (action = Action(rsf.doc.selfdoc,varlist=['rsfprefix','lang']),
               src_suffix='.c',suffix='.py')
env.Append(BUILDERS = {'Doc' : Doc})
