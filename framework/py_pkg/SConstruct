import os, sys, glob

# Madagascar modules:
import doc

try:
    import bldutil
except:
    sys.stderr.write('Run scons in RSFSRC=../.., not in this directory\n')
    Exit(1)

################################################################################

Import('env root bindir pkgdir system user')

env.Install(pkgdir,'__init__.py')

dirs = Split('plot libplot pens sumain suplot') + system + user
env.Command('prog.py',os.path.join(pkgdir,'path.py'),
            action=Action(doc.getprogs,varlist=['dirs']),dirs=dirs)

for src in Split('path doc flow proj prog tex book latex2wiki suproj use sftour'):
    bldutil.py_install(src, env, pkgdir)

env.InstallAs(bindir,'latex2wiki.pyc')
env.InstallAs(bindir,'sftour.pyc')

# Dependencies and installs from RSFSRC
Depends('proj.pyc',os.path.join(pkgdir,'bld.pyc'))

env.InstallAs(os.path.join(pkgdir,'bld.py'), 
    os.path.join('..','..','bldutil.py'))
env.InstallAs(os.path.join(pkgdir,'bld.pyc'),
    os.path.join('..','..','bldutil.pyc'))
env.Install(pkgdir,os.path.join('..','..','config.py'))
env.InstallAs(os.path.join(pkgdir,'conf.py'),
    os.path.join('..','..','configure.py'))
env.InstallAs(os.path.join(pkgdir,'conf.pyc'),
    os.path.join('..','..','configure.pyc'))

# Building rsf.use
use = env.Command('use.py',None,
                  action=Action(doc.use,varlist=['book','scons']),
                  book='book',scons=sys.argv[0])

Depends(use,map(lambda x: os.path.join(pkgdir,'sf'+x+'.py'),dirs))

for case in Split('vpplot suproj'):
    Depends(use,os.path.join(pkgdir,case+'.py'))

for case in Split('tex proj doc'):
    Depends(use,os.path.join(pkgdir,case+'.py'))

Depends(os.path.join(pkgdir,'tex.py'), os.path.join(pkgdir,'vpconvert.py'))
##########################################################################
# Self-documentation

Doc = Builder (action = Action(doc.selfdoc,varlist=['rsfprefix','lang']),
               src_suffix='.c',suffix='.py')
env.Append(BUILDERS = {'Doc' : Doc})
