import os, sys, glob, string

# Madagascar modules
dir = string.replace(os.getcwd(),'/build','')
sys.path.append(dir)

import doc

try:
    import bldutil
except:
    sys.stderr.write('Run scons in RSFSRC=../.., not in this directory\n')
    Exit(1)

################################################################################

Import('env bindir pkgdir system user')

env.Install(pkgdir,'__init__.py')

dirs = Split('plot libplot plplot pens sumain suplot') + system + user
env.Command('prog.py',os.path.join(pkgdir,'path.py'),
            action=Action(doc.getprogs,varlist=['dirs']),dirs=dirs)

for src in Split('path doc flow proj prog tex book suproj use'):
    bldutil.py_install(src, env, pkgdir)

for src in Split('latex2wiki sftour'):
    bldutil.py_install(src, env, pkgdir)
    env.Command(src,src+'.py','cp $SOURCE $TARGET')
    env.Install(bindir,src)

env.Install(bindir,'sfdoc')

# Dependencies and installs from RSFSRC
Depends('proj.py',os.path.join(pkgdir,'bld.py'))

# Building rsf.use
use = env.Command('use.py',None,
                  action=Action(doc.use,varlist=['book','scons']),
                  book='book',scons=sys.argv[0])

Depends(use,map(lambda x: os.path.join(pkgdir,'sf'+x+'.py'),dirs))

for case in Split('vpplot suproj'):
    Depends(use,os.path.join(pkgdir,case+'.py'))

for case in Split('tex proj doc'):
    Depends(use,os.path.join(pkgdir,case+'.py'))

Depends(os.path.join(pkgdir,'tex.py'), os.path.join(pkgdir,'vpconvert.py'))
##########################################################################
# Self-documentation

Doc = Builder (action = Action(doc.selfdoc,varlist=['rsfprefix','lang']),
               src_suffix='.c',suffix='.py')
env.Append(BUILDERS = {'Doc' : Doc})
