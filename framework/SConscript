import os, rsfdoc

Import('env')
    
root = env['INSTALL_PREFIX']

bindir = env['bin_prefix']
libdir = env['lib_prefix']

docdir = os.path.join(root,'doc')
txtdir = os.path.join(docdir,'txt')
mandir = os.path.join(root,'man','man1')

python = WhereIs('python')

dirs = Split('plot opengl libplot pens sumain suplot')
dirs = dirs + env['USERS']

env.GetProgs( 'rsfprog.py', None, dirs=dirs )

for src in Split('path doc flow proj prog tex book'):
    env.InstallPythonModule( 'rsf${mod}.py', mod=src)
    
env.InstallPythonModule( 'suproj.py')

for src in Split('latex2wiki vplot2eps vplot2png vplot2gif vplot2avi sftour'):
    if (src != 'vplot2gif' and src != 'vplot2avi' ) or env.get('PPM'):
        
        env.InstallPythonModule(src+'.py')
        env.InstallAs(os.path.join(bindir,src),src+'.py')

for src in Split('doc top kill'):
    env.InstallExecutable(src)
    
env.InstallExecutable('pscons')

use = env.Use( 'rsfuse.py',None, book='book' )
#use = env.Command('rsfuse.py',None,
#                  action=Action(rsfdoc.use,varlist=['book']),book='book')

Depends(use,map(lambda x: os.path.join(libdir,'sf'+x+'.py'),dirs))
for case in Split('vpplot suproj vplot2eps rsftex rsfproj'):
    Depends(use,os.path.join(libdir,case+'.py'))
    
env.InstallPythonModule('rsfuse.py')

htm = os.path.join(docdir,'index.html')
txt = os.path.join(txtdir,'INDEX.txt')
man = os.path.join(mandir,'index.man')

repos = os.environ.get('RSF_REPOSITORY',os.getcwd())
env.Append(ENV={'LD_LIBRARY_PATH':os.environ.get('LD_LIBRARY_PATH'),
                'PYTHONPATH':os.environ.get('PYTHONPATH')})
env.Command(htm,None,
            '%s framework/sfdoc -w %s -r %s' % (python,docdir,repos))
env.Command(txt,None,'%s framework/sfdoc -t %s' % (python,txtdir))
env.Command(man,None,'%s framework/sfdoc -g %s' % (python,mandir))


env.InstallPythonPackage( 'sfconfigure' )

Depends(htm,use)
Depends(txt,use)
Depends(man,use)

