import os, sys

Import('env bindir libdir pkgdir shrdir srcdir')

sys.path.insert(0,os.path.join(srcdir,'build','framework'))

env.Append(ENV={'LD_LIBRARY_PATH':os.environ.get('LD_LIBRARY_PATH')})

if env['PLATFORM'] == 'darwin':
    env.Append(ENV={'DYLD_LIBRARY_PATH':os.environ.get('DYLD_LIBRARY_PATH')})

env.Prepend(ENV={'PYTHONPATH':os.path.dirname(pkgdir)})

docdir = os.path.join(shrdir,'doc','madagascar')

txtdir = os.path.join(docdir,'txt')
htmdir = os.path.join(docdir,'html')
spcdir = os.path.join(docdir,'spec')

mandir = os.path.join(shrdir,'man')
mn1dir = os.path.join(mandir,'man1')

htm = os.path.join(htmdir,'index.html')
txt = os.path.join(txtdir,'INDEX.txt')
spc = os.path.join(spcdir,'extend.spec')
man = os.path.join(mn1dir,'index.man')

repos = os.environ.get('RSF_REPOSITORY',srcdir)

sfdoc = '../rsf/sfdoc'
python = WhereIs('python')

version = env.get('version','')
env.Command(htm,sfdoc,
            '%s $SOURCE -w %s -r %s -v %s' % (python,htmdir,repos,version))
env.Command(txt,sfdoc,'%s $SOURCE -t %s' % (python,txtdir))
env.Command(spc,sfdoc,'%s $SOURCE -s %s' % (python,spcdir))
env.Command(man,sfdoc,'%s $SOURCE -g %s' % (python,mn1dir))

uses = os.path.join(pkgdir,'use.py')

env.Depends(htm,uses)
env.Depends(txt,uses)
env.Depends(spc,uses)
env.Depends(man,uses)
