EnsureSConsVersion(0, 96)

import os, sys, glob
from os.path import abspath,join
#sys.path.append(abspath('framework'))
import sfconfigure

env = Environment()

try:
    env.Tool('rsfroot')
except:
    raise SystemExit('Please run ./configure first.')

root = env['INSTALL_PREFIX']
env['RSFROOT']=root

bindir = env['bin_prefix']
libdir = env['lib_prefix']
incdir = env['include_prefix']
docdir = os.path.join(root,'doc')
mandir = os.path.join(root,'man')

##########################################################################
# CONFIGURATION
##########################################################################

env.InstallAs(os.path.join(libdir,'rsfconfig.py'),'config.py')

##########################################################################
# PYTHON BUILD
##########################################################################

user = filter(lambda x: x[0] != '.' and x != 'nobody', os.listdir('user'))
env['USERS']=user

Export('env')

env.Tool('rsfutil')
env.Tool('sfdoc')
env.Append(LIBS=[])

SConscript(dirs='framework',name='SConstruct')

##########################################################################
# API BUILD
##########################################################################

env.Append( CPPPATH=['#/build/api/c'],
            LIBPATH=['#/build/api/c'],
            LIBS=['rsf'])

for dir in glob.glob('api/[a-z]*'):
    build = os.path.join('build',dir)
    BuildDir(build,dir)
    SConscript(dirs=build,name='SConscript')
    Default(build)

##########################################################################
# USER BUILD
##########################################################################
for dir in map(lambda x: os.path.join('user',x), user):
    build = os.path.join('build',dir)
    BuildDir(build,dir)
    SConscript(dirs=build,name='SConscript')
    Default(build)

##########################################################################
# PLOT BUILD
##########################################################################
pdirs = ('lib','main','test','opengl')

for dir in map(lambda x: os.path.join('plot',x), pdirs):
    build = os.path.join('build',dir)
    BuildDir(build,dir)
    SConscript(dirs=build,name='SConscript')
    Default(build)

##########################################################################
# PENS BUILD
##########################################################################
pdirs = ('fonts','include','utilities','genlib','main','docs')

for dir in map(lambda x: os.path.join('pens',x), pdirs):
    build = os.path.join('build',dir)
    BuildDir(build,dir)
    SConscript(dirs=build,name='SConscript')
    Default(build)

##########################################################################
# SU BUILD
##########################################################################
sudirs = ('lib','main','plot')

for dir in map(lambda x: os.path.join('su',x), sudirs):
    build = os.path.join('build',dir)
    BuildDir(build,dir)
    SConscript(dirs=build,name='SConscript')
    Default(build)

##########################################################################
# INSTALLATION
##########################################################################

rsfuser = os.path.join(libdir,'rsfuser')
env.Install(rsfuser,'__init__.py')


env.InstallPythonPackage( 'sfconfigure' )
env.InstallPythonPackage( 'sfconfigure/tools' )
env.InstallPythonPackage( 'sfconfigure/custom_builders' )


env.Alias('install',[incdir,bindir,libdir,rsfuser,docdir,mandir])
env.Clean('install', rsfuser)

#   $Id: SConstruct 3987 2008-12-16 19:52:35Z srossross $
