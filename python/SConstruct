import os, rsfdoc

Import('env')

root = env.get('RSFROOT')

bindir = os.path.join(root,'bin')
libdir = os.path.join(root,'lib')
docdir = os.path.join(root,'doc')
txtdir = os.path.join(docdir,'txt')

python = WhereIs('python')

dirs = Split('main proc imag plot opengl libplot pens sumain suplot')
dirs = dirs + env['USERS']

env.Command('rsfprog.py',None,
            action=Action(rsfdoc.getprogs,varlist=['dirs']),dirs=dirs)

for src in ('doc','proj','prog','tex','book','bak'):
    py = 'rsf%s.py' % src
    env.Install(libdir,py)
# clean .pyc files?

env.Install(libdir,'suproj.py')

for src in ('latex2wiki','vplot2eps','vplot2gif','vplot2avi','sftour'):
    if (src != 'vplot2gif' and src != 'vplot2avi' ) or env.get('PPM'):
        env.Install(libdir,src+'.py')
        env.InstallAs(os.path.join(bindir,src),src+'.py')

for src in ('doc','units','top','kill'):
    env.Install(bindir,'sf'+src)
env.Install(bindir,'pscons')

use = os.path.join(libdir,'rsfuse.py')
env.Command(use,None,action=Action(rsfdoc.use,varlist=['book']),book='book')
Depends(use,map(lambda x: os.path.join(libdir,'sf'+x+'.py'),dirs))
Depends(use,os.path.join(libdir,'vpplot.py'))
Depends(use,os.path.join(libdir,'suproj.py'))
Depends(use,os.path.join(libdir,'vplot2eps.py'))

htm = os.path.join(docdir,'index.html')
txt = os.path.join(txtdir,'INDEX.txt')
repos = os.environ.get('RSF_REPOSITORY',os.getcwd())
env.Append(ENV={'LD_LIBRARY_PATH':os.environ.get('LD_LIBRARY_PATH'),
                'PYTHONPATH':os.environ.get('PYTHONPATH')})
env.Command(htm,None,
            '%s python/sfdoc -w %s -r %s' % (python,docdir,repos))
env.Command(txt,None,'%s python/sfdoc -t %s' % (python,txtdir))
Depends(htm,use)
Depends(txt,use)

##########################################################################
# SELF DOCUMENTATION
##########################################################################
Doc = Builder (action = Action(rsfdoc.selfdoc,varlist=['rsfprefix','lang']),
               src_suffix='.c',suffix='.py')
env.Append(BUILDERS = {'Doc' : Doc})
