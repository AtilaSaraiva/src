import os, sys, string
sys.path.append('../..')
import configure

plots = '''
7ab cmp croshyp csp dc dcretard ellipse
headray hyplay nmotraj reflector reflexpt reflkine sg vrms whitepruf
'''

try:
	Import('env')
	env = env.Copy()
	
	root = env.get('RSFROOT')
	libdir = os.path.join(root,'lib')
	bindir = os.path.join(root,'bin')
	incdir = os.path.join(root,'include')
except:
	root = None
	env = Environment()
	opts=Options('../../config.py')
	configure.options(opts)
	opts.Update(env)
	env['CCFLAGS'] = string.replace(env.get('CCFLAGS',''),'-O2','-g')
	SConscript('../lib/SConstruct')

env.Prepend(CPPPATH=['../../include'],
	    LIBPATH=['../../filt/lib','../lib'],
	    LIBS=['rsfplot','rsf'])

vplot = Builder(action = './$SOURCE - > $TARGET',suffix='.vpl')
env.Append(BUILDERS = {'Vplot' : vplot})
env['PROGPREFIX'] = 'vp'

mains = []

for plot in Split(plots):
	if plot in ['reflexpt','reflkine']:
		env.Object(plot+'.o','reflector.c',
			   CCFLAGS=env.get('CCFLAGS','') +
			   ' -D%s' % string.upper(plot))
		prog = env.Program(plot+'.o')
	else:
		prog = env.Program(plot+'.c')
		mains.append(plot+'.c')
	if root:
		env.Install(bindir,prog)
	elif plot == 'sg':
		env.Command('geqs.vpl', prog,'./$SOURCE 1 > $TARGET')
		env.Command('geq2s.vpl',prog,'./$SOURCE 2 > $TARGET')
	else:
		env.Vplot(plot,prog)

######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:
	main = 'vpplot.py'
    
	docs = map(lambda prog: env.Doc(prog,rsfprefix='vp'),mains)
	docmerge = configure.docmerge
	docmerge = configure.docextra(docmerge,'vpreflector','vpreflexpt')
	docmerge = configure.docextra(docmerge,'vpreflector','vpreflkine')
	doc = env.Command(main,docs,docmerge)
	env.Install(libdir,doc)
	Clean(os.path.join(libdir,main),os.path.join(libdir,main+'c'))
#####################################################################


# 	$Id$	
