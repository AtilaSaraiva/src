import os, sys

src = "axis coltab device gainpar genpen plot polygon stdplot vplot"

sources  = map(lambda x: x+'.c',Split(src)) 
includes = map(lambda x: x+'.h',Split(src))

root = os.environ.get('RSFROOT') or '../..'
libdir = os.path.join(root,'lib')
incdir = os.path.join(root,'include')


try:
    Import('env')
except:
    env = Environment(CCFLAGS='-std=gnu9x -Wall -pedantic -g',
                      CPPPATH=incdir,
                      LIBPATH=libdir,
                      LIBS=['rsf','m'])
    env.Alias('install',[libdir,incdir])

sys.path.append(root)
from rsfdoc import merge

lib = env.StaticLibrary('rsfplot',sources)
inc = env.Command('rsfplot.h',includes,Action(merge))

env.Install(libdir,lib)
env.Install(incdir,inc)

##############################################################################
# TESTING
##############################################################################
for file in ('axis','stdplot'):
    test = 'Test' + file + '.c'
    env.Program([test,lib],PROGSUFFIX='.x',PROGPREFIX='')
##############################################################################
