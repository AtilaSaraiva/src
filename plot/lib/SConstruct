import os, sys
sys.path.append('../..')
import configure

#############################################################################
# MAIN LIBRARY
#############################################################################
src = "axis coltab device gainpar genpen plot polygon stdplot vplot"

sources  = map(lambda x: x+'.c',Split(src)) 
includes = map(lambda x: x+'.h',Split(src))

try: # distribution version
    Import('env')
    root = env.get('RSFROOT')
    libdir = os.path.join(root,'lib')
    incdir = os.path.join(root,'include')
except: # local version
    root = None
    env = Environment()
    opts=Options('../../config.py')
    configure.options(opts)
    opts.Update(env)
    env['CCFLAGS'] = env.get('CCFLAGS','').replace('-O2','-g')
    env.Append(CPPPATH=['../../filt/lib'])
    SConscript('../../filt/lib/SConstruct')

lib = env.StaticLibrary('rsfplot',sources)
inc = env.Command('rsfplot.h',includes,Action(configure.merge))
install = env.Install('../../include',inc)

if root:
    env.Install(libdir,lib)
    env.Install(incdir,inc)
else:
    pass
#    env.Alias('install',install)

##############################################################################
# TESTING
##############################################################################
for file in ('axis','stdplot'):
    test = 'Test' + file + '.c'
    libs = ['rsf','m']
    if sys.platform[:5] == 'sunos':
	libs.append('nsl')
    env.Program([test,lib],PROGSUFFIX='.x',PROGPREFIX='',
                LIBS=libs,LIBPATH='../../filt/lib')
##############################################################################
