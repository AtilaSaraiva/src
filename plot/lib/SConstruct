import os, sys, string
sys.path.append('../..')
import configure

#############################################################################
# MAIN LIBRARY
#############################################################################
psrc = Split('axis coltab contour gainpar plot stdplot vplot')

try: # distribution version
    Import('env root libdir incdir')
    env = env.Clone()
except: # local version
    env = configure.Debug()
    root = None
    SConscript('../../api/c/SConstruct')

env.Prepend(CPPPATH=['../../include'],
            LIBPATH=['../../lib'])

##############################################################################
# INCLUDES
##############################################################################
includes = map(lambda x: x+'.h',psrc)

objects = []
dobjects = []
for source in psrc:
    env.Include(source,prefix='vp_')

    obj = env.StaticObject(source+'.c')
    env.Depends(obj,source+'.h')
    objects.append(obj)

    obj = env.SharedObject(source+'.c')
    env.Depends(obj,source+'.h')
    dobjects.append(obj)

##############################################################################
# MAIN LIBRARY
##############################################################################
slib = env.StaticLibrary('rsfplot',objects)
dlib = env.SharedLibrary('rsfplot',dobjects,
                         SHLIBPREFIX='libd',
                         LIBS=['drsf']+env.get('LIBS',[]))

inc = env.Command('rsfplot.h',includes,Action(configure.merge))
env.Install('../../lib',[slib,dlib])
env.Install('../../include',inc)

if root:
    install = env.Install(libdir,[slib,dlib])
    env.Install(incdir,inc)

    if env['PLATFORM'] == 'darwin':                
        env.AddPostAction(install,
                          'install_name_tool -change '
                          'build/api/c/libdrsf.dylib '
                          '%s/libdrsf.dylib %s/%s' % \
                              (libdir,libdir,dlib[0]))

api = env.get('API',[])

##############################################################################
# FORTRAN-77 BINDING
##############################################################################
if 'f77' in api:
    env.Object('fortran.o','fortran.c',
               CCFLAGS=env.get('CCFLAGS')+' -D$CFORTRAN')
    libf = env.StaticLibrary('rsfplotf','fortran.o')
    if root:
        env.Install(libdir,libf)

##############################################################################
# TESTING
##############################################################################
for file in ('axis','stdplot'):
    test = 'Test' + file + '.c'
    env.Program([test,slib],PROGSUFFIX='.x',PROGPREFIX='',
                LIBS=['rsf']+env.get('LIBS',[]))
        
if root:
    main = 'sflibplot.py'

    docs = map(lambda prog: env.Doc(prog),('stdplot.c',))
    env.Depends(docs,'#/framework/rsfdoc.py')

    doc = env.Docmerge(main,docs)
    env.Install(libdir,doc)
    
# 	$Id$	

