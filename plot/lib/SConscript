
#############################################################################
# MAIN LIBRARY
#############################################################################
psrc = Split('axis coltab contour gainpar plot stdplot vplot')


Import('env')
env = env.Clone()

env.Tool( 'rsfc' ) # paths


env.Tool('rsfutil') # builders
env.Tool('sfdoc')   # builders

env.Tool( 'rsfcc') # compiler and flags

##############################################################################
# INCLUDES
##############################################################################
includes = map(lambda x: x+'.h',psrc)

objects = []
dobjects = []
for source in psrc:
    env.Include(source,prefix='vp_')

    obj = env.StaticObject(source+'.c')
    env.Depends(obj,source+'.h')
    objects.append(obj)

    obj = env.SharedObject(source+'.c')
    env.Depends(obj,source+'.h')
    dobjects.append(obj)

##############################################################################
# MAIN LIBRARY
##############################################################################
slib = env.StaticLibrary('rsfplot',objects)
dlib = env.SharedLibrary('rsfplot',dobjects,
                         SHLIBPREFIX='libd',LIBS=['drsf']+env.get('LIBS'))

inc = env.Merge('rsfplot.h',includes)

env.InstallLibrary([slib,dlib])
env.InstallInclude(inc)

##############################################################################
# FORTRAN-77 BINDING
##############################################################################

f77env = env.Clone()

f77env.Tool('rsff77')
f77env.Append(CPPDEFINES='$CFORTRAN')

f77env.Object('fortran.o','fortran.c')
libf = f77env.StaticLibrary('rsfplotf','fortran.o')
    
env.InstallLibrary(libf)

##############################################################################
# TESTING
##############################################################################
for file in ('axis','stdplot'):
    test = 'Test' + file + '.c'
    env.Program([test,slib],PROGSUFFIX='.x',PROGPREFIX='')
        
if env.get('root'):
    
    main = 'sflibplot.py'

    docs = map(lambda prog: env.Doc(prog),('stdplot.c',))
    env.Depends(docs,'#/framework/rsfdoc.py')

    doc = env.Docmerge(main,docs)
    env.Install("library_prefix", doc)
    
#     $Id: SConstruct 3987 2008-12-16 19:52:35Z srossross $    

