import os, sys, string
sys.path.append('../..')
import configure

progs = 'box contour dots graph grey grey3 plas pldb plotrays thplot wiggle'

try:
    Import('env')
    root = env.get('RSFROOT')
    libdir = os.path.join(root,'lib')
    bindir = os.path.join(root,'bin')
    incdir = os.path.join(root,'include')
    docmerge = configure.docmerge
    alias = {'grey':'byte','grey3':'cubeplot'}
except:
    root = None
    env = Environment()
    opts=Options('../../config.py')
    configure.options(opts)
    opts.Update(env)
    env['CCFLAGS'] = env.get('CCFLAGS','').replace('-O2','-g')
    env.Prepend(CPPPATH=['../../include'],
                LIBPATH=['../../filt/lib','../lib'],LIBS=['rsfplot','rsf'])
    SConscript('../lib/SConstruct')

mains = Split(progs)

for prog in mains:
    main = env.Program(prog + '.c')
    if root:
         env.Install(bindir,main[0])
         if prog in alias.keys():
	       copy = string.replace(str(main[0]),prog,alias[prog])
               env.InstallAs(os.path.join(bindir,copy),main[0])
               doc = os.path.splitext(copy)[0]
	       docmerge = configure.docextra(docmerge,'sf'+prog,doc)

######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:
    main = 'sfplot.py'
    
    docs = map(lambda prog: env.Doc(prog),mains)
    env.Depends(docs,'#/python/rsfdoc.py')

    doc = env.Command(main,docs,docmerge)
    env.Install(libdir,doc)
    Clean(os.path.join(libdir,main),os.path.join(libdir,main+'c'))
#####################################################################

# 	$Id$

