import os, sys, string
sys.path.append('../../framework')
import bldutil

try:  # distributed version
    Import('env root pkgdir bindir')
    env = env.Clone()
except: # local version
	env = bldutil.Debug()
	srcroot = os.environ.get('RSFSRC', '../..')
	root = None
	SConscript('../../api/c/SConstruct')



# these must be defined for your specific system
MPI_LIB = '/usr/lib/openmpi/lib'		# MPI lib directory
MPI_INC = '/usr/lib/openmpi/include'		# MPI include directory



progs = '''
ewefd2d_gpu
ewefd3d_gpu_p2p
ewefd3d_gpu_mpi
'''



CTP = env.get('CUDA_TOOLKIT_PATH')
NVCC = env.get('NVCC')
mpicc = WhereIs('mpicc')

env.Append(LIBS=[env.get('DYNLIB','')+'rsf'],
           CPPPATH=['../../include'],
           LIBPATH=['../../lib'])

if CTP:
    env.Append(LIBS=['cuda','cudart'],
               LIBPATH=[os.path.join(CTP,'lib64'),os.path.join(CTP,'lib')])


mains = Split(progs)
for prog in mains:
    if CTP and NVCC:
        cfile = env.Command(prog+'.c','M'+prog+'.cu','cp $SOURCE $TARGET')
        prog = env.Program(cfile,
                           CC=NVCC,
                           LINKFLAGS='-L'+MPI_LIB+' -lmpi',
                           CFLAGS='-I'+MPI_INC+' -prec-div=true -arch=sm_21 --x=cu')
    else:
        prog = env.RSF_Place('sf'+prog,None,
                             message='''
                             Please check if CUDA is not missing and is installed correctly.
                             Rerun Madagascar installation after it has been fixed.
                             ''')
    if root:
        env.Install(bindir,prog)


######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:
    user = os.path.basename(os.getcwd())
    main = 'sf%s.py' % user

    docs = map(lambda prog: env.Doc(prog,'M' + prog+'.cu'),mains)
    env.Depends(docs,'#/framework/rsf/doc.py')	
    
    doc = env.RSF_Docmerge(main,docs)
    env.Install(pkgdir,doc)

