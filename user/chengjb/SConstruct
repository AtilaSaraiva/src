import os, sys, re, string
sys.path.append('../../framework')
import bldutil

progs = '''
vti2dpseudop vti2dpseudop1 vti2dpseudopfvs0
tti2dpseudop tti2dpseudop1
ort3dpseudop
vti2delasticsep vti2delasticsepupdown
tti2delasticsep
tti3delastic
ort3dhomodevK ort3dhomodevcK
kine2dvti
datasucjb2rsf2d datasucjb2rsf3d datasucjb2rsf3dnh
twolayer2dti threecolumn2dti
'''

ccprogs = '''
vti2delasticlrsep vti2delasticlrsep2ps
tti2delasticlrsep tti2delasticlrsep1p tti2delasticlrsep2p tti2delasticlrsep2ps
vti2dpseudoplrsep vti2dpseudoplrsep1
tti3delasticlrsep
tti2delasticlrdecomp tti2delasticlrdecomp1p tti2delasticlrdecomp2p
'''
#isoonewaypsdmlr

pyprogs ='''
'''
 
pymods = '''
'''

tprogs = '''
fftw
'''

subs = '''
'''
#lowrankcjb

try:  # distributed version
    Import('env root pkgdir bindir libdir incdir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    root = None
    SConscript('../lexing/SConstruct')  # include lexing's head and lib files

env.Prepend(CPPPATH=['../../include'],
            LIBPATH=['../../lib'],
            LIBS=[env.get('DYNLIB','')+'rsf'])

##############################################################################
# ADD FFTW3 LIB
##############################################################################
fftw = env.get('FFTW')
if fftw:
    env.Prepend(CPPDEFINES=['SF_HAS_FFTW'],
                LIBS=[env.get('DYNLIB','')+fftw])

if 'c++' in env.get('API',[]):
    lapack = env.get('LAPACK')
    if lapack:
        lapackpp=[env.get('DYNLIB','')+'rsf++','vecmatop']+lapack
        env.Append(LIBS=lapack)
else:
    lapack = None

##############################################################################
# Compiling C programs 
##############################################################################
src = Glob('[a-z]*.c')
objs = []
for source in src:
    inc = env.RSF_Include(source,prefix='')
    obj = env.StaticObject(source)
    env.Depends(obj,inc)
    objs.append(obj)

mains = Split(progs)
for prog in mains:
    sources = ['M' + prog]
    bldutil.depends (env, sources, 'M'+prog)
    env.StaticObject('M'+prog+'.c')
    if prog in ('ort3dhomodevK',) and not lapack:
        prog = env.RSF_Place('sf'+prog,None,var='LAPACK',package='lapack')
    else:
        prog = env.Program (prog, map (lambda x: x + '.c',sources))
    if root:
        env.Install (bindir, prog)

##############################################################################
# Compiling C++ programs 
##############################################################################

#subs    = Split(subs)
#if 'c++' in env.get('API',[]):
#        obj = map(lambda x:  env.StaticObject(x+'.cc'), subs)
#        objs.append(obj)

ccmains = Split(ccprogs)
for prog in ccmains:
    sources = ['M' + prog] 
    if lapack:
        prog = env.Program(prog, map(lambda x: x + '.cc',sources)+objs, LIBS=lapackpp+env.get('LIBS'))
    else:
        prog = env.RSF_Place('sf'+prog,None,var='LAPACK',package='lapack')
    if root:
        env.Install(bindir,prog)

##############################################################################
# Compiling C programs (Test)
##############################################################################
for prog in Split(tprogs):
        sources = ['T'+prog]
        bldutil.depends(env,sources,'T'+prog)
        env.StaticObject('T'+prog+'.c')
        prog = env.Program(prog,map(lambda x: x + '.o',sources),PROGPREFIX='T',PROGSUFFIX='.x')

######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:
    user = os.path.basename(os.getcwd())
    main = 'sf%s.py' % user
    
    docs = map(lambda prog: env.Doc(prog,'M' + prog),mains) +  \
           map(lambda prog: env.Doc(prog,'M%s.cc' % prog,lang='c++'),ccmains) 
    env.Depends(docs,'#/framework/rsf/doc.py')	

    doc = env.RSF_Docmerge(main,docs)
    env.Install(pkgdir,doc)
