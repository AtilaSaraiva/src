import os, sys, re, string, glob
rsfsrc = os.environ.get('RSFSRC','../..')
sys.path.append(rsfsrc)
import configure

progs = '''
aimplfd1
aimplfd2
heatgmres1
'''

try:  # distributed version
    Import('env root libdir bindir')
    env = env.Clone()
    dir = string.replace(os.getcwd(),'/build','')
    src = map(os.path.basename,glob.glob(os.path.join(dir,'[a-z]*.c')))
except: # local version
    env = configure.Debug()
    root = None
    SConscript(os.path.join(rsfsrc,'api/c/SConstruct'))
    src = glob.glob('[a-z]*.c')

petscdir = env.get('PETSCDIR')

if petscdir:
    petsccc = env.get('PETSCCC')
    petscpath = env.get('PETSCPATH',[])
    petsclibpath = env.get('PETSCLIBPATH',[])
    petsclibs = env.get('PETSCLIBS',[])
        
    env.Replace(CC=petsccc)
    env.Prepend(CPPPATH=petscpath,
                LIBPATH=petsclibpath,
                LIBS=petsclibs)

    env.Prepend(CPPPATH=[os.path.join(rsfsrc,'include')],
                LIBPATH=[os.path.join(rsfsrc,'lib')],
                LIBS=['rsf'])

    for source in src:
        inc = env.Include(source,prefix='')
        obj = env.StaticObject(source)
        env.Depends(obj,inc)
    
mains = Split(progs)
for prog in mains:
    if petscdir:
        sources = ['M' + prog]
        configure.depends(env,sources,'M'+prog)
        prog = env.Program(prog,map(lambda x: x + '.c',sources))
    else:
        prog = env.Place('sf'+prog,None,var='PETSCDIR',package='petsc')
    if root:
        env.Install(bindir,prog)

######################################################################
# SELF-DOCUMENTATION
######################################################################
if petscdir and root:
    user = os.path.basename(os.getcwd())
    main = 'sf%s.py' % user

    docs = map(lambda prog: env.Doc(prog,'M' + prog),mains)
    env.Depends(docs,'#/framework/rsfdoc.py')	
    
    doc = env.Docmerge(main,docs)
    env.Install(libdir,doc)

