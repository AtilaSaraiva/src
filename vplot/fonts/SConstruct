import os, sys, glob, string
sys.path.append('../..')
import configure

try: # distributed version
    Import('env')
    root = env.get('RSFROOT')
    incdir = os.path.join(root,'include')
    dir = string.replace(os.getcwd(),'/build','')
    src = map(os.path.basename,glob.glob(os.path.join(dir,'[a-z]*.vplot_font')))
except: # local version
    root = None
    env = Environment()
    opts=Options('../../config.py')
    configure.options(opts)
    opts.Update(env)
    src = glob.glob('[a-z]*.vplot_font')

prog = env.Program('makefont','makefont.c',PROGSUFFIX='.x',PROGPREFIX='')

for font in src:
    base = os.path.splitext(font)[0]
    include = base+'.include'
    bin = base+'.bin'
    if root:
        top = os.path.join(dir,'../../')
    else:
        top = ''
        
    aux = map(lambda x: '%s%s_%s' % (top,base,x),
                  ('check','header','dim','addr','widthl','widthr','symbol','svec','lig'))
    env.Command([include,]+aux,[prog,font],
                './${SOURCES[0]} %s < ${SOURCES[1]} > ${TARGETS[0]}' % base)
    env.Command(bin,aux,'cat $SOURCES > $TARGET')
    if root:
        env.Install(incdir,include)
        env.Install(incdir,bin)
