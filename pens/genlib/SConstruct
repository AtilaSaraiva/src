import os, sys, string
sys.path.append('../..')
import configure

src = '''
genarea genhatch geninteract genmarker genmessage genpatarea genpoint
genraster1 genraster gentext genvector nulldev polyfix polysubs vecarea
'''

try: # distribution version
    Import('env root libdir incdir')
    env = env.Clone()
except: # local version
    env = configure.Debug()
    root = None
    incdir = os.path.join(os.environ.get('RSFROOT','/usr'),'include')        
    SConscript('../fonts/SConstruct')
    SConscript('../utilities/SConstruct')

env.Prepend(CPPPATH=['../../include'])

sources  = map(lambda x: x+'.c',Split(src))
includes = map(lambda x: x+'.h',Split(src))

incdir = os.environ.get('VPLOTFONTDIR',incdir+os.sep)

objects = []
for source in Split(src):
    env.Include(source,prefix='pen_')
    obj = env.StaticObject(source+'.c',
                           CPPFLAGS=env.get('CPPFLAGS',[])+['-DVPLOT_FONT_DIR=\'\"%s\"\'' % incdir,])
    env.Depends(obj,source+'.h')
    objects.append(obj)

inc = env.Command('genpen.h',includes,Action(configure.merge))
lib = env.StaticLibrary('genpen',objects)

