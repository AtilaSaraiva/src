import os, sys, string, types
sys.path.append('../..')
import configure

src = 'do  init_  main_ proc_'

try: # distribution version
    Import('env root libdir bindir')
    env = env.Clone()
except: # local version
    env = configure.Debug()
    root = None
    SConscript('../genlib/SConstruct')
    
env.Prepend(CPPPATH=['../../include'],
            LIBPATH=['../utilities','../genlib','../../lib'],
            LIBS=['genpen','penutil','rsfplot','rsf'])

env['PROGPREFIX'] = ''

sources = map(lambda x: x+'vplot',Split(src))

objects = []
for source in sources:
    env.RSF_Include(source,prefix='pen_')
    obj = env.StaticObject(source+'.c')
    env.Depends(obj,source+'.h')
    objects.append(obj)

docalias = {}
sfpen = env.get('SFPEN')

# vppen
#######

env.RSF_Include('vppen',prefix='pen_')
obj = env.StaticObject('vppen.c')
env.Depends(obj,'vppen.h')
main = env.Program('vppen',[obj,]+objects)

if root:
    env.Install(bindir,main[0])
        
pens = ['vppen']

# xtpen
#######

inc = env.get('XINC')
lib = env.get('XLIBPATH')

if inc and lib:
    env.RSF_Include('xtpen',prefix='pen_')
    obj = env.StaticObject('xtpen.c',CPPPATH=env.get('CPPPATH',[])+inc)
    env.Depends(obj,'xtpen.h')
    main = env.Program('xtpen',[obj,]+objects,
                       LIBPATH=env.get('LIBPATH',[])+lib,
                       LIBS=env.get('XLIBS',[])+env.get('LIBS',[]))
    pens.append('xtpen')
else:
    main = env.RSF_Place('xtpen',None,
                     var='XINC and XLIBPATH',package='libXaw-devel')

if root:
    env.Install(bindir,main[0])
    if 'xtpen' == sfpen:
        env.InstallAs(os.path.join(bindir,'sfpen'),main[0])
        docalias['sfpen']='sfxtpen'

# x11pen
#######

if inc and lib:
    env.RSF_Include('x11pen',prefix='pen_')
    obj = env.StaticObject('x11pen.c',CPPPATH=env.get('CPPPATH',[])+inc)
    env.Depends(obj,'x11pen.h')
    main = env.Program('x11pen',[obj,]+objects,
                       LIBPATH=env.get('LIBPATH',[])+lib,
                       LIBS=env.get('XLIBS',[])+env.get('LIBS',[]))
    pens.append('x11pen')
else:
    main = env.RSF_Place('x11pen',None,var='XINC and XLIBPATH')
    
if root:
    env.Install(bindir,main[0])

# pspen
#######

env.RSF_Include('pspen',prefix='pen_')
obj = env.StaticObject('pspen.c')
env.Depends(obj,'pspen.h')
main = env.Program('pspen',[obj,]+objects)

if root:
    env.Install(bindir,main[0])

pens.append('pspen')

# raspen
#######

env.RSF_Include('raspen',prefix='pen_')
pens.append('raspen')

for pen in ('ppm','tiff','jpeg'):
    cap = pen.upper()
    name = pen+'pen'
    lib = env.get(cap)

    if lib:
        path = env.get(cap+'PATH')
        defines = env.get('CPPDEFINES',[])+['_'+cap]
        if path:    
            obj = env.StaticObject(name+'.o','raspen.c',
                                   CPPPATH=env.get('CPPPATH',[])+[path],
                                   CPPDEFINES=defines)
        else:
            obj = env.StaticObject(name+'.o','raspen.c',
                                   CPPDEFINES=defines)
        env.Depends(obj,'raspen.h')
        main = env.Program(name,[obj,]+objects,
                           LIBS=[lib]+env.get('LIBS',[]))
        docalias['sf'+name]='sfraspen'
    else:
        main = env.RSF_Place(name,None,var=cap,
                         package={'ppm':'libnetpbm-devel',
                                  'tiff':'libtiff-devel',
                                  'jpeg':'libjpeg-devel'}[pen])

    if root:
        env.Install(bindir,main[0])

# gdpen
#######

gd = env.get('GD')
# Ability to save animated GIFS
gifanim = env.get('GIFANIM')
# Ability to save MPEG movies
ffmpeg = env.get('FFMPEG')

if gd:
    env.RSF_Include('gdpen',prefix='gd_')
    path =  env.get('CPPPATH',[])
    defines = env.get('CPPDEFINES',[])
    libs = [gd]+env.get('LIBS',[])

    if gifanim:
        defines += ['GIFANIM']
    
    if ffmpeg:
        ffmpegpath = env.get('FFMPEGPATH',[])
        if type(ffmpegpath) is not types.ListType:
            ffmpegpath = Split(ffmpegpath)
        path += ffmpegpath
        defines += ['FFMPEG']
        libs += [ffmpeg]
        
    obj = env.StaticObject('gdpen.o','gdpen.c',
                           CPPPATH=path,
                           CPPDEFINES=defines)
    env.Depends(obj,'gdpen.h')
    main = env.Program('gdpen',[obj,]+objects,LIBS=libs)
    pens.append('gdpen')
else:
    main = env.RSF_Place('gdpen',None,var='GD',package='libgd2-noxpm-dev')
    
if root:
    env.Install(bindir,main[0])

# crpen
#######

env.RSF_Include('crpen',prefix='pen_')
pens.append('crpen')
path = env.get('CAIROPATH')

for pen in ('png','pdf','svg'):
    cap = pen.upper()
    name = pen+'pen'
    lib = env.get('CAIRO'+cap)

    if lib:
        defines = env.get('CPPDEFINES',[])+['_'+cap]
        if path:    
            obj = env.StaticObject(name+'.o','crpen.c',
                                   CPPPATH=env.get('CPPPATH',[])+[path],
                                   CPPDEFINES=defines)
        else:
            obj = env.StaticObject(name+'.o','crpen.c',
                                   CPPDEFINES=defines)
        env.Depends(obj,'crpen.h')
        main = env.Program(name,[obj,]+objects,
                           LIBS=[lib]+env.get('LIBS',[]))
        docalias['sf'+name]='sfcrpen'
    else:
        main = env.RSF_Place(name,None,var='CAIRO-'+cap,pakage='cairo')
    
    if root:
        env.Install(bindir,main[0])

# oglpen
#######

ogl = env.get('OPENGL')
oglflags = env.get('OPENGLFLAGS')
oglpath = env.get('OPENGLPATH')

if ogl or oglflags:
    if type(ogl) is not types.ListType:
        ogl = Split(ogl)
    if oglflags:
        if type(oglflags) is not types.ListType:
            oglflags = Split(oglflags)
    env.RSF_Include('oglpen',prefix='ogl_')
    obj = env.StaticObject('oglpen.c',
                           CPPPATH=[oglpath]+env.get('CPPPATH',[]))
    env.Depends(obj,'oglpen.h')
    main = env.Program('oglpen',[obj,]+objects,
                       LIBS=[ogl]+env.get('LIBS',[]),
                       LINKFLAGS=[oglflags]+env.get('LINKFLAGS',[]))
    pens.append('oglpen')
else:
    main = env.RSF_Place('oglpen',None,var='OPENGL',package='freeglut-devel')

if root:
    env.Install(bindir,main[0])
    if 'oglpen' == sfpen:
        env.InstallAs(os.path.join(bindir,'sfpen'),main[0])
        docalias['sfpen']='sfoglpen'

######################################################################
# SELF-DOCUMENTATION
######################################################################
if root:    
    main = 'sfpens.py'

    for pen in pens:
        env.Command('M'+pen+'.c',[pen+'.c','init_vplot.c'],
                    'cat $SOURCES > $TARGET')
        if pen == env.get('SFPEN'):
            env.Command('Msfpen.c',[pen+'.c','init_vplot.c'],
                    'cat $SOURCES > $TARGET')
    
    docs = map(lambda prog: env.Doc(prog,'M'+prog),pens)
    env.Depends(docs,'#/framework/rsfdoc.py')
    doc = env.RSF_Docmerge(main,docs,alias=docalias)
    env.Install(libdir,doc)
 
#####################################################################

# 	$Id: SConstruct,v 1.1 2009/06/04 19:38:44 fomels Exp $	
