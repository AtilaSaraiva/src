import os, sys, glob, string
sys.path.append('../..')
import bldutil

try: # distributed version
    Import('env root incdir')
    env = env.Clone()
    dir = string.replace(os.getcwd(),'/build','')
    src = map(os.path.basename,glob.glob(os.path.join(dir,'[a-z]*.vplot_font')))
except: # local version
    env = bldutil.Debug()
    root = None
    src = glob.glob('[a-z]*.vplot_font')

prog = env.Program('makefont','makefont.c',PROGSUFFIX='.x',PROGPREFIX='')

attr = ('check','header','dim','addr','widthl','widthr','symbol','svec','lig')

if root:
    top = os.path.join(dir,'../../')
else:
    top = ''
  
for font in src:
    base = os.path.splitext(font)[0]
    include = base+'.include'
    bin = base+'.bin'
       
    aux = string.join(map(lambda x: '%s%s_%s' % (top,base,x),attr))
                          
    env.Command([include,bin],[prog,font],
                'LD_LIBRARY_PATH=%s ${SOURCES[0].abspath} %s '
                '< ${SOURCES[1]} > ${TARGETS[0]} && '
                'cat %s > ${TARGETS[1]} && '
                'rm %s' % (os.environ.get('LD_LIBRARY_PATH',''),base,aux,aux))

    env.Install('../../include',include)

    if root:
        env.Install(incdir,include)
        env.Install(incdir,bin)
