import sys, os, string, re
sys.path.append('../..')
import bldutil

modules = Split('rsf vplot')

try: # distribution version
    Import('env root libdir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    root = None
    SConscript('../../plot/lib/SConstruct')

env.Prepend(CPPPATH=['../../include'],
            LIBPATH=['../../lib'])

if 'gomp' in env['LIBS']:
    env['LIBS'].remove('gomp')

if 'python' in env.get('API',[]):
    try:
        EnsureSConsVersion(1,0)
    except:
        print "\n\n\tPlease upgrade SCons to version >= 1.0\n\n"
        sys.exit(1)

    # Python includes for SWIG
    pythinc = []
    try:
        import numpy
        numpy_loc = os.path.split(numpy.__file__)[0]
        pythinc.append(os.path.join(numpy_loc,'numarray','numpy'))
        pythinc.append(os.path.join(numpy_loc,'core','include'))
    except:
        print "\n\n\tPlease install numpy\n\n"
        sys.exit(1)

    import distutils.sysconfig
    pythinc.append(distutils.sysconfig.get_python_inc())

    env.Append(CPPPATH=pythinc)

    if env['PLATFORM'] == 'cygwin':
        python = 'python%s' % sys.version[:3]
        env.Prepend(LIBPATH=['/usr/lib/%s/config' % python],
                    LIBS = ['librsfplot','librsf',python])
    else:
        env.Append(LIBS=['drsfplot','drsf'])

    for module in modules:
        ifile = module+'.i'
        wrap = env.CFile(module,ifile,SWIGFLAGS='-python')
        python = env.LoadableModule(module,wrap[0],
                                    LDMODULEPREFIX='_c_',
                                    LDMODULESUFFIX='.so',
                                    FRAMEWORKSFLAGS='-flat_namespace -undefined suppress')

        pfiles = [python,wrap[1],module+'.py']
        env.Install('../../lib',pfiles)

        if root:
            install = env.Install(libdir,pfiles)
            if env['PLATFORM'] == 'darwin':
                for lib in ('rsf','rsfplot'):
                    env.AddPostAction(install,
                                      'install_name_tool -change '
                                      'build/%s/libd%s.dylib '
                                      '%s/libd%s.dylib %s/%s' % \
                                          ({'rsf':'api/c',
                                            'rsfplot':'plot/lib'}[lib],
                                           lib,libdir,lib,libdir,python[0]))
