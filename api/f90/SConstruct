import sys, os
sys.path.append('../..')
import configure

try: # distribution version
    Import('env root libdir incdir')
    env = env.Clone()
except: # local version
    env = configure.Debug()
    root = None
    SConscript('../c/SConstruct')

env.Prepend(CPPPATH=['../../include'],
            LIBPATH=['../../lib'],
            LIBS=['rsf'])

if 'f90' in env.get('API',[]) and env.has_key('F90'):
    F90 = env.get('F90')
    F90base =  os.path.basename(F90)
    env.Append(CPPDEFINES=['$CFORTRAN90'])

    if F90base[:8] == 'gfortran' or F90base[:3] == 'gfc':
        env.Append(CPPDEFINES=['GFORTRAN'],F90FLAGS=' -J${SOURCE.dir}')
    elif F90base == 'ifort':
        env.Append(CPPDEFINES=['INTEL_COMPILER'],F90FLAGS=' -module ${SOURCE.dir}')

    env.Object('fortran.o','fortran.c')
    obj = env.Object('rsf.f90')

    lib = env.StaticLibrary('rsff90',[obj[0],'fortran.o'])
    mod = obj[1]

    env.Install('../../lib',lib)
    env.Install('../../include',mod)
    
    if root:
        env.Install(libdir,lib)
        env.Install(incdir,mod)

    for file in ['getpar','file']:
        test = 'Test' + file + '.f90'
        env.Program(file,[test,lib],
                    PROGSUFFIX='.x',
                    PROGPREFIX='Test',
                    LINK=F90,
                    LINKFLAGS=env.get('F90FLAGS'))
