import sys, os
sys.path.append('../..')
import bldutil

sources = Split('rsf cub vai')

try: # distribution version
    Import('env root libdir incdir')
    env = env.Clone()
except: # local version
    env = bldutil.Debug()
    root = None
    SConscript('../c/SConstruct')

env.Prepend(CPPPATH=['../../include'],
            LIBPATH=['../../lib'],
            LIBS=['rsf'])

if 'c++' in env.get('API',[]):
    ccfiles = map(lambda x: x+'.cc',sources)
    hhfiles = map(lambda x: x+'.hh',sources)

    lib = env.StaticLibrary('rsf++',ccfiles)
    env.Install('../../lib',lib)
    env.Install('../../include',hhfiles)

    if root:
        env.Install(libdir,lib)
        env.Install(incdir,hhfiles)

    for file in ['getpar','file']:
        test = 'Test' + file + '.cc'
        env.Program(file,[test,lib],PROGSUFFIX='.x',PROGPREFIX='Test')
