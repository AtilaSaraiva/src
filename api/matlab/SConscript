import  os, string, re

Import('env')
env = env.Clone()

if 'rsf' in env.get('LIBS',[]):
    env['LIBS'].remove('rsf')
    
env.Prepend(LIBS=['drsf'])

env.Tool('rsfmatlab')

mex = env.get('MEX')

for inp in Split('par dim read write create'):
    cfile = 'rsf_' + inp

    if mex:
        mfile = env.Command( cfile+"$MEXSUFFIX", cfile+'.c',
                             "${MEX} CFLAGS='${_CPPINCFLAGS} ${CCFLAGS} -fPIC' "
                             "$SOURCE ${_LIBDIRFLAGS} ${_LIBFLAGS} "
                             "-output $TARGET")

        install = env.InstallLibrary(mfile)

        if env['PLATFORM'] == 'darwin':
            env.AddPostAction(install,
                              'install_name_tool -change '
                              'build/api/c/libdrsf.dylib '
                              '${lib_prefix}/libdrsf.dylib ${lib_prefix}/%s' % \
                                  (mfile[0]))

