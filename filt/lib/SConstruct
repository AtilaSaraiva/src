import os, sys, string
sys.path.append('../..')
import configure

#############################################################################
# MAIN LIBRARY
#############################################################################
src = 'kiss_fft kiss_fftr'

src2 = '''
adjnull alloc bigsolver c99 ccgstep cconjgrad cell cgstep chain conjgrad
conjprec decart dottest eno eno2 error file files freqfilt getpar math1 pqueue
quadratic segy simtab stack
'''

try: # distribution version
    Import('env')
    root = env.get('RSFROOT')
    libdir = os.path.join(root,'lib')
    incdir = os.path.join(root,'include')
except: # local version
    root = None
    env = Environment()
    opts=Options('../../config.py')
    configure.options(opts)
    opts.Update(env)
    env['CCFLAGS'] = string.replace(env.get('CCFLAGS',''),'-O2','-g')

#env['SCANNERS'].remove(CScan)
env.Append(BUILDERS={'Include':configure.Header},
           SCANNERS=[configure.Include])

##############################################################################
# INCLUDES
##############################################################################
sources  = map(lambda x: x+'.c',Split(src))
includes = map(lambda x: x+'.h',Split(src2)+Split(src))

objects = []
for source in Split(src2):
    env.Include(source,prefix='sf_')
    obj = env.StaticObject(source+'.c')
    env.Depends(obj,source+'.h')
    objects.append(obj)

##############################################################################
# MAIN LIBRARY
##############################################################################
lib = env.StaticLibrary('rsf',objects+sources)
inc = env.Command('rsf.h',includes,Action(configure.merge))
install = env.Install('../../include',inc)
   
if root:
    env.Install(libdir,lib)
    env.Install(incdir,inc)
else:
    env.Alias('install',install)

##############################################################################
# TESTING
##############################################################################
for file in [
    'eno2','file','getpar','simtab']:
    test = env.StaticObject('Test' + file + '.c')
    prog = env.Program([test,lib],PROGSUFFIX='.x',PROGPREFIX='')
    env.Depends(test,includes) # remove later 

api = string.split(string.lower(env.get('API','')),',')

##############################################################################
# C++ BINDING
##############################################################################
if 'c++' in api:
    libcc = env.StaticLibrary('rsf++','rsf.cc',
                              OBJSUFFIX='++.o',CPPPATH='../../include')
    if root:
        env.Install(libdir,libcc)
        env.Install(incdir,'rsf.hh')

    for file in ['getpar','file']:
        test = 'Test' + file + '.cc'
        
        env.Program([test,libcc,lib],
                    OBJSUFFIX='++.o',CPPPATH='../../include',
                    PROGSUFFIX='.x',PROGPREFIX='')
        
##############################################################################
# FORTRAN-77 BINDING
##############################################################################
if 'fortran' in api:
    env.Object('fortran.o','fortran.c',
               CCFLAGS=env.get('CCFLAGS')+' -D$CFORTRAN')
    libf = env.StaticLibrary('rsff','fortran.o')                         
    
    if root:
        env.Install(libdir,libf)
        
    for file in ['getpar','file']:
        test = 'Test' + file + '.f'
        env.Program([test,libf,lib],PROGSUFFIX='.x',PROGPREFIX='',
                    OBJSUFFIX='f.o',
                    LINK=env.get('F77'),LINKFLAGS=env.get('F77FLAGS'))
        
##############################################################################
# FORTRAN-90 BINDING
##############################################################################
if 'fortran-90' in api or 'fortran90' in api or 'f90' in api:
    if env.has_key('F90'):
        import configure

        env.Object('fortran90.o','fortran.c',
                   CCFLAGS=env.get('CCFLAGS')+' -D$CFORTRAN90')
        env.Object('rsf.o','rsf.f90')
        libf90 = env.StaticLibrary('rsff90',['rsf.o','fortran90.o'])
        
        if root:
            env.Install(libdir,libf90)
            
        for file in ['getpar','file']:
            test = 'Test' + file + '.f90'
            env.Program([test,libf90,lib],PROGSUFFIX='f90.x',PROGPREFIX='',
                        OBJSUFFIX='f90.o',
                        LINK=env.get('F90'),LINKFLAGS=env.get('F90FLAGS'))
            
##############################################################################
# PYTHON BINDING
##############################################################################
if 'python' in api and 'swig' in env.get('TOOLS'):
    # Python includes for SWIG 
    pythinc = []
    for path in sys.path:
        if string.find(path,'python') >= 0:
            dir = string.replace(path,'lib','include')
            if os.path.isdir(dir):
                pythinc.append(dir)

    python = env.SharedLibrary('rsf',['rsf.i']+sources,
                               SWIGFLAGS='-python',
                               CPPPATH=pythinc,
                               SHLIBPREFIX='_c_')

    if root:
        env.Install(libdir,python)
        env.Install(libdir,'c_rsf.py')
        env.Install(libdir,'rsf.py')
        Clean(python,['c_rsf.py','c_rsf.pyc'])

##############################################################################
# LINT
##############################################################################

env.Command('../lint/lint',map(lambda x: x+'.c',Split(src2)),
            'splint +posixlib $SOURCES')
env.Alias('lint','../lint')
        
env['SCANNERS'].remove(configure.Include)
#env.Append(SCANNERS=CScan)
