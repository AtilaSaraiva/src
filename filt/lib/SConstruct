import os, sys, string

#############################################################################
# MAIN LIBRARY
#############################################################################
src = "alloc c99 decart error file files getpar math1 pfafft segy simtab stack"

sources  = map(lambda x: x+'.c',Split(src))
includes = map(lambda x: x+'.h',Split(src))

root = os.environ.get('RSFROOT') or '../..'
libdir = os.path.join(root,'lib')
incdir = os.path.join(root,'include')

try:
    Import('env')
except:
    env = Environment(CCFLAGS='-std=gnu9x -Wall -pedantic -g')
    env.Alias('install',[libdir,incdir])

sys.path.append(libdir)
from rsfdoc import merge

lib = env.StaticLibrary('rsf',sources)
env.Command('rsf.h',includes,Action(merge))

env.Install(libdir,lib)
env.Install(incdir,'rsf.h')

##############################################################################
# PYTHON BINDINGS
##############################################################################

# Python includes for SWIG
pythinc = []
for path in sys.path:
    if string.find(path,'python') >= 0:
        dir = string.replace(path,'lib','include')
        if os.path.isdir(dir):
            pythinc.append(dir)

python = env.SharedLibrary('rsf',['rsf.i',lib],
                           SWIGFLAGS='-python',
                           CPPPATH=pythinc,
                           SHLIBPREFIX='_c_')
env.Install(libdir,python)
env.Install(libdir,'c_rsf.py')
env.Install(libdir,'rsf.py')
Clean(python,['c_rsf.py','c_rsf.pyc'])

##############################################################################
# TESTING
##############################################################################
for file in ('file','getpar','simtab'):
    test = 'Test' + file + '.c'
    env.Program([test,lib],PROGSUFFIX='.x',PROGPREFIX='')
##############################################################################

